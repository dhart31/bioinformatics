
rule all:
    input:
        "multiqc_report.html"

rule fastp_trim:
    input:
        r1 = "raw_data/{sample}_R1.fastq",
        r2 = "raw_data/{sample}_R2.fastq"
    output:
        r1 = "fastp_trim/{sample}_trimmed_R1.fastq.gz",
        r2 = "fastp_trim/{sample}_trimmed_R2.fastq.gz",
        json = "fastp_trim/{sample}.json",
        html = "fastp_trim/{sample}.html"
    conda:
        "fastp"
    log:
        "fastp_trim/{sample}.log"
    params:
        version = "software_mqc_versions.yaml"
    shell:
        """
        fastp \
        -i {input.r1} -I {input.r2} \
        -o {output.r1} -O {output.r2} \
        -j {output.json} -h {output.html} \
        2> {log};

        version=$(fastp --version 2>&1 | cut -f 2 -d ' ');
        echo "fastp: "$version > {params.version}
        """

rule bowtie2_align:
    input:
        r1 = "fastp_trim/{sample}_trimmed_R1.fastq.gz",
        r2 = "fastp_trim/{sample}_trimmed_R2.fastq.gz"
    output:
        bam = "bowtie2_align/{sample}.bam"
    conda:
        "bowtie2"
    threads: 16
    log:
        "bowtie2_align/{sample}.log"
    params:
        ref = "~/references/AF266287",
        version = "software_mqc_versions.yaml"
    shell:
        """
        bowtie2 -x {params.ref} -1 {input.r1} -2 {input.r2} \
        --threads {threads} --sensitive-local 2> {log} |
        samtools sort - > {output.bam};

        echo "bowtie2_align:" >> {params.version}
        version=$(bowtie2 --version 2>&1 | head -n 1 | cut -f 3 -d ' ');
        echo "  bowtie2: "$version >> {params.version}
        version=$(samtools --version 2>&1 | head -n 1 | cut -f 2 -d ' ');
        echo "  samtools: "$version >> {params.version}
        """
rule samtools_rmdup:
    input:
        bam = "bowtie2_align/{sample}.bam"
    output:
        bam = "samtools_rmdup/{sample}_rmdup.bam"
    log:
        "samtools_rmdup/{sample}_rmdup.log"
    conda:
        "samtools"
    params:
        version = "software_mqc_versions.yaml"
    shell:
        """
        samtools rmdup {input.bam} {output.bam} > {log} 2>&1;

        version=$(samtools --version 2>&1 | head -n 1 | cut -f 2 -d ' ');
        echo "samtools: "$version >> {params.version}
        """

rule bedtools_genomecov:
    input:
        bam = "samtools_rmdup/{sample}_rmdup.bam"
    output:
        "bedtools_genomecov/{sample}.bed"
    conda:
        "bedtools"
    log:
        "bedtools_genomecov/{sample}.log"
    params:
        version = "software_mqc_versions.yaml"
    shell:
        """
        bedtools genomecov -d -ibam {input.bam} > {output} 2> {log};

        version=$(bedtools --version 2>&1 | head -n 1 | cut -f 2 -d ' ');
        echo "bedtools: "$version >> {params.version}
        """

rule plot_coverage:
    input:
        bedfiles = expand("bedtools_genomecov/{sample}.bed",sample=['AF266287_sample1'])
    output:
        "plot_coverage/coverage_across_reference_mqc.html"
    conda:
        "plot"
    params:
        script = f"{workflow.basedir}/scripts/plot_coverage.py",
        version = "software_mqc_versions.yaml"
    shell:
        """
        python {params.script} -i {input.bedfiles} -o {output} -v {params.version};
        """

rule multiqc_report:
    input: 
        expand("fastp_trim/{sample}.json",sample=['AF266287_sample1']),
        expand("samtools_rmdup/{sample}_rmdup.bam",sample=['AF266287_sample1']),
        expand("bedtools_genomecov/{sample}.bed",sample=['AF266287_sample1']),
        "plot_coverage/coverage_across_reference_mqc.html"

    output:
        "multiqc_report.html"
    conda:
        "multiqc"
    params: 
        config = f"{workflow.basedir}/config/multiqc.yaml"
    shell:
        """
        multiqc --config {params.config} -f .;
        version=$(multiqc --version | cut -f3 -d' ');
        echo "multiqc: "$version >> software_mqc_versions.yaml
        """
